package automotive_system
public

with security_properties;

bus internet
end internet;

bus can
end can;

bus wifi
end wifi;

bus cellular
end cellular;

system attacker
prototypes
	network : bus;
features
	connection : requires bus access network;
end attacker;

device router
prototypes
	routertype : bus;
features
	socket   : requires bus access routertype;
	internet : requires bus access internet;
end router;




system function
features
	can_socket : requires bus access can;
end function;

system radio
features
	can_ihs_socket : requires bus access can;
	can_c_socket   : requires bus access can;
	wifi_access    : requires bus access wifi;
	cellular_access : requires bus access cellular;
end radio;

system car
features
	cellular_access : requires bus access cellular;
	wifi_access     : provides bus access wifi;
end car;


system implementation car.cherokee
subcomponents
    --  The radio system - connected to the wireless, cellular
    --  and two cans networks.
	radio : system radio {security_properties::security_level => 1;};
	
	--  The CAN buses: IHS is connected to non-critical
	--  systems, the C is connected to critical equipments.
	can_ihs      : bus can {security_properties::security_level => 2;};
	can_c        : bus can {security_properties::security_level => 1;};
	
	--  The wifi provided by the car to connect to the car
	wifi        : bus wifi;
	
	-- Functions on the IHS (non-critical) bus
	--  temperature control
	hvac        : system function {security_properties::security_level => 2;};  
	
	--  radio amplifier
	amplifier  	: system function {security_properties::security_level => 2;};  
	--  body control module
	bcm         : system function {security_properties::security_level => 2;}; 
	--  heated seats 
	hsm         : system function {security_properties::security_level => 2;};  
	
	-- Functions on the C (critical) bus
	--  braking
	abs : system function {security_properties::security_level => 1;};
	--  adaptive cruise control
	acc : system function {security_properties::security_level => 1;};
	--  electric power steering  
	eps : system function {security_properties::security_level => 1;};
	--  electric parking brake
	epb : system function {security_properties::security_level => 1;};    
connections
    -- Connections for all the non-critical functions on the IHS bus
	ihs_radio      : bus access can_ihs <-> radio.can_ihs_socket;
	ihs_hvac       : bus access can_ihs <-> hvac.can_socket;
	ihs_amplifier  : bus access can_ihs <-> amplifier.can_socket;
	ihs_bcm        : bus access can_ihs <-> bcm.can_socket;
	ihs_hsm        : bus access can_ihs <-> hsm.can_socket;
	
	-- Connections for all the critical function on the C bus
	c_radio : bus access can_c <-> radio.can_c_socket;
	c_abs   : bus access can_c <-> abs.can_socket;
	c_acc   : bus access can_c <-> acc.can_socket;
	c_eps   : bus access can_c <-> eps.can_socket;
	c_epb   : bus access can_c <-> epb.can_socket;
	
	wifi_radio      : bus access wifi <-> radio.wifi_access;
	wifi_outside    : bus access wifi <-> wifi_access;
	cellular_radio  : bus access cellular_access <-> radio.cellular_access;
end car.cherokee;

--
--  Generic integration component
--

system integration
end integration;

--
--  Integration of the system with its operating environment.
--

system implementation integration.i
subcomponents
	car         : system car.cherokee;
	router_cel  : device router (routertype => bus cellular);
	cellular    : bus cellular;
	internet    : bus internet;
connections
	car_cel      : bus access cellular <-> car.cellular_access;
	
	--  The router is connected to the internet AND the cellular
	routercel    : bus access cellular <-> router_cel.socket;
	routercelint : bus access internet <-> router_cel.internet;
end integration.i;

--
--  This model adds attackers that would then be able to attack
--  the system. We connect these persons to the shared resources.
--

system implementation integration.attack extends integration.i
subcomponents
	attacker_cel      : system attacker (network => bus cellular)
						{security_properties::security_level => 100;};
	attacker_wifi     : system attacker (network => bus wifi)
						{security_properties::security_level => 100;};
	attacker_internet : system attacker (network => bus internet)
						{security_properties::security_level => 100;};
connections
	attacker_to_internet : bus access attacker_internet.connection <-> internet;
	attacker_to_cel      : bus access attacker_cel.connection <-> cellular;
	attacker_to_wifi     : bus access attacker_wifi.connection <-> car.wifi_access;
annex resolute {**
	prove (check_component (this))
**};
end integration.attack;


end automotive_system;
