package emsoft2016::solutions::exercise4::integration
public
	with emsoft2016::solutions::exercise4::software;
	with emsoft2016::solutions::exercise4::devices;
	with emsoft2016::solutions::exercise3::integration;
	with emsoft2016::solutions::exercise4::platform;

	system integration extends emsoft2016::solutions::exercise3::integration::integration
	end integration;

	system implementation integration.functional extends emsoft2016::solutions::exercise3::integration::integration.functional
	subcomponents
		heater : refined to device emsoft2016::solutions::exercise4::devices::heater;
		cooler : refined to device emsoft2016::solutions::exercise4::devices::cooler;
		sensor0   : refined to device emsoft2016::solutions::exercise4::devices::sensor;
		sensor1   : refined to device emsoft2016::solutions::exercise4::devices::sensor;
	    filtering : refined to process emsoft2016::solutions::exercise4::software::filtering.i;
	    ac        : refined to process emsoft2016::solutions::exercise4::software::air_conditioner.i;
	annex EMV2 {**
		use types errorlibrary;
		use behavior errorlibrary::failstop;

		composite error behavior
		states
			[heater.failstop or cooler.failstop]-> failstop;
			[sensor0.failstop and sensor1.failstop]-> failstop;
		end composite;
	**}; 	 
	end integration.functional;

	system implementation integration.local extends integration.functional
	subcomponents
		cpu : processor emsoft2016::solutions::exercise4::platform::arm;
		discrete_sensor0 : bus emsoft2016::solutions::exercise4::platform::discrete;
		discrete_sensor1 : bus emsoft2016::solutions::exercise4::platform::discrete;
		discrete_heater : bus emsoft2016::solutions::exercise4::platform::discrete;
		discrete_cooler : bus emsoft2016::solutions::exercise4::platform::discrete;		
	properties
		actual_processor_binding => (reference (cpu)) applies to filtering;
		actual_processor_binding => (reference (cpu)) applies to ac;
		actual_connection_binding => (reference (discrete_sensor0)) applies to sensor0_to_filter;
		actual_connection_binding => (reference (discrete_sensor1)) applies to sensor1_to_filter;
		actual_connection_binding => (reference (discrete_heater)) applies to ac_to_heater;
		actual_connection_binding => (reference (discrete_cooler)) applies to ac_to_cooler;
	end integration.local;

	system implementation integration.distributed extends integration.functional
	subcomponents
		cpu0 : processor emsoft2016::solutions::exercise4::platform::arm;
		cpu1 : processor emsoft2016::solutions::exercise4::platform::arm;
		eth0 : bus emsoft2016::solutions::exercise4::platform::ethernet;
		discrete_sensor0 : bus emsoft2016::solutions::exercise4::platform::discrete;
		discrete_sensor1 : bus emsoft2016::solutions::exercise4::platform::discrete;
		discrete_heater : bus emsoft2016::solutions::exercise4::platform::discrete;
		discrete_cooler : bus emsoft2016::solutions::exercise4::platform::discrete;
	connections
		cpu0_eth : bus access eth0 <-> cpu0.eth_socket;
		cpu1_eth : bus access eth0 <-> cpu1.eth_socket;
	properties
		actual_processor_binding => (reference (cpu0)) applies to filtering;
		actual_processor_binding => (reference (cpu1)) applies to ac;
		actual_connection_binding => (reference (eth0)) applies to filtering_to_ac;
		actual_connection_binding => (reference (discrete_sensor0)) applies to sensor0_to_filter;
		actual_connection_binding => (reference (discrete_sensor1)) applies to sensor1_to_filter;
		actual_connection_binding => (reference (discrete_heater)) applies to ac_to_heater;
		actual_connection_binding => (reference (discrete_cooler)) applies to ac_to_cooler;
	end integration.distributed;	

end emsoft2016::solutions::exercise4::integration;